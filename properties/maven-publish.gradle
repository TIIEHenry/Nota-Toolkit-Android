apply plugin: 'maven-publish'


task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

project.afterEvaluate {
    publishing {
        publications {
            Module(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version

                println "publish:" + project.name + ":" + project.version
                // 上传 AAR 包
                afterEvaluate { artifact(tasks.getByName("bundleReleaseAar")) }
                // 向 Maven 仓库中上传源码
                artifact sourceJar
                pom.withXml {
                    //在pom.xml添加依赖关系
                    def dependenciesNode = asNode().appendNode('dependencies')
                    //拿到所有编译时的依赖
                    def dependenciesList = []
                    dependenciesList.addAll(configurations.implementation.allDependencies)
                    dependenciesList.addAll(configurations.api.allDependencies)
                    dependenciesList.each {
                        if (null != it.group) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'implementation')
                        }
                    }

                }
            }
        }
    }
}

